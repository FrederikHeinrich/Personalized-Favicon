import org.apache.tools.ant.taskdefs.condition.Os

plugins {
    id 'java'
    id 'net.kyori.blossom' version  '1.3.1'
    id 'com.github.johnrengelman.shadow' version '8.1.1'
    id 'io.freefair.lombok' version '8.1.0'
}

group = 'io.freddi.personalizedfavicon'

def versionPropsFile = file('version.properties')
def versionProps = new Properties()

if (versionPropsFile.exists()) {
    versionProps.load(new FileInputStream(versionPropsFile))
}

// Die Versionsnummer aus der version.properties-Datei abrufen
def version = versionProps['version'] ?: 'unknown'

defaultTasks 'shadowJar'

repositories {
    mavenCentral()
    maven {
        url 'https://jitpack.io'
    }
    maven {
        name = 'papermc'
        url = 'https://repo.papermc.io/repository/maven-public/'
    }
    maven { url = "https://repo.papermc.io/repository/maven-public/" }
    maven { url = "https://hub.spigotmc.org/nexus/" }
    maven {url = "https://hub.spigotmc.org/nexus/content/repositories/snapshots/"}
}

dependencies {
    //Velocity
    compileOnly 'com.velocitypowered:velocity-api:3.1.1'
    annotationProcessor 'com.velocitypowered:velocity-api:3.1.1'

    //Bungeecord
    compileOnly 'net.md-5:bungeecord-api:1.20-R0.1-SNAPSHOT'

    //MongoDB
    implementation 'org.mongodb:mongodb-driver-sync:4.10.1'
    implementation 'dev.morphia.morphia:morphia-core:2.3.5'

    //Adventure
    implementation "net.kyori:adventure-text-minimessage:4.14.0"
    implementation "net.kyori:adventure-platform-bungeecord:4.3.0"

    // https://mvnrepository.com/artifact/org.apache.commons/commons-lang3
    implementation 'org.apache.commons:commons-lang3:3.0'



}

def getGitCommitDescription() {
    return 'git rev-parse --short HEAD'.execute().text.trim()
}

blossom {
    replaceToken("@version@", version +"-"+ getGitCommitDescription())
}

configurations {
    shadow
}

shadowJar{
    archiveBaseName = 'PersonalizedFavicon'
    archiveVersion = version
    archiveClassifier = getGitCommitDescription()
}

processResources {
    def props = [version: version +"-" + getGitCommitDescription()]
    inputs.properties props
    filteringCharset 'UTF-8'
    outputs.upToDateWhen { false }
    filesMatching('plugin.yml'){
        expand props
    }
    filesMatching('bungee.yml'){
        expand props
    }
    filesMatching('paper-plugin.yml'){
        expand props
    }
}